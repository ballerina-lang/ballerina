apply from: "$rootDir/gradle/balNativeLibProject.gradle"

configurations {
    cliUtils
}

dependencies {
    baloImplementation project(path: ':ballerina-builtin', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-utils', configuration: 'baloImplementation')

    baloImplementation project(path: ':ballerina-internal', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-io', configuration: 'baloImplementation')

    // transitive
    baloImplementation project(path: ':ballerina-time', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-log-api', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-encoding', configuration: 'baloImplementation')
    baloImplementation project(path: ':ballerina-runtime-api', configuration: 'baloImplementation')

    implementation project(':ballerina-lang')
    implementation project(':ballerina-core')

    implementation project(':ballerina-builtin')
    implementation project(':ballerina-runtime-api')
    implementation project(':ballerina-io')
    implementation project(':ballerina-encoding')
    implementation project(':ballerina-log-api')
    implementation project(':ballerina-time')
    implementation project(':ballerina-internal')
    implementation project(':ballerina-utils')

    testCompile 'io.netty:netty-transport:4.1.34.Final'
    testCompile 'org.testng:testng:6.13.1'
    testCompile project(':ballerina-launcher')
    testCompile 'org.slf4j:slf4j-jdk14:1.7.26'

    cliUtils project(':ballerina-cli-utils')
}

description = 'Ballerina - BIR Model'

task genBalx(type: JavaExec) {

    doFirst {
        copy {
            from "$buildDir/generated-balo/repo/ballerina"
            into "$buildDir/lib/repo/ballerina"
        }
    }
    dependsOn createBalo

    classpath = configurations.cliUtils
    main = 'org.ballerinalang.cli.utils.GenerateBalx'

    def src = "$buildDir/../src/test/ballerina/bir_test"
    def fileName = "bir_test.bal"
    def target = "$buildDir/bir_test.balx"
    args("$src", "$fileName", "$target", "$buildDir")

    def balFiles = file("src/main/ballerina/")
    inputs.dir(balFiles)
    inputs.files(new File(src, fileName))
    inputs.files(configurations.baloImplementation)
    outputs.files(target)
    outputs.cacheIf { true }
}

test {
    dependsOn genBalx

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
}
