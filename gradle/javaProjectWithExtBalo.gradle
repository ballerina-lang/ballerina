/*
 * Copyright (c) 2019, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */
 
apply from: "$rootDir/gradle/javaProject.gradle"

configurations {
    baloCreat
    baloImplementation {
        transitive false
    }
    baloTestImplementation {
        extendsFrom baloImplementation
        transitive false
    }
}

dependencies {
//    baloCreat project(':lib-creator')
    annotationProcessor project(':ballerina-lang')

    baloTestImplementation project(path: ':ballerina-bir', configuration: 'baloImplementation')
    baloTestImplementation project(path: ':ballerina-reflect', configuration: 'baloImplementation')
    baloTestImplementation project(path: ':ballerina-jvm', configuration: 'baloImplementation')
    baloTestImplementation project(path: ':ballerina-encoding', configuration: 'baloImplementation')
    baloTestImplementation project(path: ':ballerina-internal', configuration: 'baloImplementation')
    baloTestImplementation project(path: ':ballerina-io', configuration: 'baloImplementation')
    baloTestImplementation project(path: ':ballerina-builtin', configuration: 'baloImplementation')
    baloTestImplementation project(path: ':ballerina-utils', configuration: 'baloImplementation')

}

compileJava {
    def generatedSources = "$buildDir/generated-from-annot"
    def generatedOutputDir = file("$generatedSources")
    def pkgName = project.name.replace("ballerina", "").replaceAll("-","")
    options.compilerArgs += ["-AnativeEntityProviderClass=StandardNativeElementProvider",
                             "-AnativeEntityProviderPackage=org.ballerinalang.${pkgName}.generated.providers",
                             "-s", "$generatedSources"]

    doFirst {
        generatedOutputDir.exists() || generatedOutputDir.mkdirs()
    }
}

task createBalHome(type: org.gradle.api.tasks.Copy) {
    from configurations.baloImplementation
    into "$buildDir/ballerina-home/main/lib"
}

task createTestBalHome(type: Copy) {
    from configurations.baloTestImplementation
    into "$buildDir/ballerina-home/test/lib/"
}

test {
    dependsOn createTestBalHome

    inputs.files(configurations.baloTestImplementation);

    systemProperty "ballerina.home", "$buildDir/ballerina-home/test"
    systemProperty "enableJBallerinaTests", "true"

    systemProperty "java.util.logging.config.file", "src/test/resources/logging.properties"
    systemProperty "java.util.logging.manager", "org.ballerinalang.logging.BLogManager"

    useTestNG() {
        suites 'src/test/resources/testng.xml'
    }
    minHeapSize = "256m"
    maxHeapSize = "1024m"
}

